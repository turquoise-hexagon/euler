(import
  (chicken fixnum))

(define (make-sum-divisors n)
  (let ((acc (make-vector (fx+ n 1) 0)))
    (let loop ((i 1))
      (unless (fx> i n)
        (let subloop ((m (fx+ i i)))
          (unless (fx> m n)
            (vector-set! acc m (fx+ (vector-ref acc m) i))
            (subloop (fx+ m i))))
        (loop (fx+ i 1))))
    (define (sum-divisors n)
      (vector-ref acc n))
    sum-divisors))

(define (abundants n)
  (let ((sum-divisors (make-sum-divisors n)))
    (let loop ((i 0) (acc '()))
      (if (fx> i n)
        acc
        (loop (fx+ i 1)
          (if (fx> (sum-divisors i) i)
            (cons i acc)
            acc))))))

(define (make-valid? n)
  (let ((acc (make-vector (fx+ n 1) #t)) (lst (abundants n)))
    (for-each
      (lambda (i)
        (for-each
          (lambda (j)
            (let ((_ (fx+ i j)))
              (unless (fx> _ n)
                (vector-set! acc _ #f))))
          lst))
      lst)
    (define (valid? n)
      (vector-ref acc n))
    valid?))

(define (solve n)
  (let ((valid? (make-valid? n)))
    (do ((i 0 (fx+ i 1))
         (acc 0 (if (valid? i)
                  (fx+ i acc)
                  acc)))
      ((fx> i n) acc))))

(let ((_ (solve 28123)))
  (print _) (assert (= _ 4179871)))
